prototype(Wwwision.Form.FusionPreset:FormElement) < prototype(Neos.Fusion:Array) {
    label = Neos.Fusion:Tag {
        tagName = 'label'
        attributes {
            for = ${element.uniqueIdentifier}
        }
        content = ${element.label}
        content.@process.requiredFlag = ${value + '<span class="required">*</span>'}
        content.@process.requiredFlag.@if.isRequired = ${element.required}
    }
    fieldContainer = Neos.Fusion:Array {
        field = Neos.Fusion:Tag {
            tagName = 'input'
            attributes {
                placeholder = ${element.properties.placeholder}
                class = Neos.Fusion:Array {
                    defaultClass = ${element.properties.elementClassAttribute ? element.properties.elementClassAttribute : null}
                    errorClass = ${element.properties.elementErrorClassAttribute ? element.properties.elementErrorClassAttribute : 'error'}
                    errorClass.@process.wrap = ${' ' + value}
                    errorClass.@if.hasValidationError = ${FusionPreset.Form.hasValidationErrors(formRuntime, element)}
                }
                id = ${element.uniqueIdentifier}
                type = 'text'
                name = ${'--' + formRuntime.identifier + '[' + element.identifier + ']'}
                value = ${FusionPreset.Form.elementValue(formRuntime, element)}
            }

        }
        validationErrors = Neos.Fusion:Tag {
            tagName = 'span'
            attributes {
                class = 'help-inline'
            }
            content = Neos.Fusion:Collection {
                collection = ${FusionPreset.Form.validationErrors(formRuntime, element)}
                itemName = 'error'
                itemRenderer =  ${error}
            }
            @if.hasValidationErrors = ${FusionPreset.Form.hasValidationErrors(formRuntime, element)}
        }
        elementDescription = Neos.Fusion:Tag {
            tagName = 'span'
            attributes {
                class = 'help-block'
            }
            content = ${element.properties.elementDescription}
            @if.condition = ${element.properties.elementDescription}
        }

        @process.wrap = Neos.Fusion:Tag {
            tagName = 'div'
            attributes.class = ${element.properties.containerClassAttribute}
            content = ${value}
        }
    }

    @process.renderCallbacks = Wwwision.Form.FusionPreset:RenderCallbacks {
        formRuntime = ${formRuntime}
        formElement = ${element}
    }

    @process.wrap = Neos.Fusion:Tag {
        tagName = 'div'
        attributes.class = ${FusionPreset.Form.hasValidationErrors(formRuntime, element) ? 'clearfix error' : 'clearfix'}
        content = ${value}
    }

}