prototype(Wwwision.Form.FusionPreset:FormElement) < prototype(Neos.Fusion:Array) {
    @context.elementName = ${'--' + formRuntime.identifier + '[' + element.identifier + ']'}
    @context.elementValue = ${FusionPreset.Form.elementValue(formRuntime, element)}
    @context.elementValicationErrors = ${FusionPreset.Form.validationErrors(formRuntime, element)}
    @context.elementHasValicationErrors = ${FusionPreset.Form.hasValidationErrors(formRuntime, element)}

    label = Wwwision.Form.FusionPreset:FormElementLabel
    fieldContainer = Wwwision.Form.FusionPreset:FormElementFieldContainer

    @process.renderCallbacks = Wwwision.Form.FusionPreset:RenderCallbacks {
        formRuntime = ${formRuntime}
        formElement = ${element}
    }

    @process.wrap = Neos.Fusion:Tag {
        tagName = 'div'
        attributes.class = ${hasValidationErrors ? 'clearfix error' : 'clearfix'}
        content = ${value}
    }

}

prototype(Wwwision.Form.FusionPreset:FormElementLabel) < prototype(Neos.Fusion:Tag) {
    tagName = 'label'
    attributes {
        for = ${element.uniqueIdentifier}
    }
    content = Neos.Fusion:Array {
        value = ${element.label}
        requiredFlag = Wwwision.Form.FusionPreset:RequiredFlag
        requiredFlag.@if.isRequired = ${element.required}
    }
}

prototype(Wwwision.Form.FusionPreset:RequiredFlag) < prototype(Neos.Fusion:Tag) {
    tagName = 'span'
    attributes.class = 'required'
    content = ${value + '*'}
}

prototype(Wwwision.Form.FusionPreset:FormElementFieldContainer) < prototype(Neos.Fusion:Array) {
    field = Wwwision.Form.FusionPreset:FormElementField
    validationErrors = Wwwision.Form.FusionPreset:FormElementValidationErrors
    elementDescription = Wwwision.Form.FusionPreset:FormElementDescription

    @process.wrap = Neos.Fusion:Tag {
        tagName = 'div'
        attributes.class = ${element.properties.containerClassAttribute}
        content = ${value}
    }
}

prototype(Wwwision.Form.FusionPreset:FormElementField) < prototype(Neos.Fusion:Tag) {
    attributes {
        placeholder = ${element.properties.placeholder}
        class = Neos.Fusion:Array {
            defaultClass = ${element.properties.elementClassAttribute ? element.properties.elementClassAttribute : null}
            errorClass = ${element.properties.elementErrorClassAttribute ? element.properties.elementErrorClassAttribute : 'error'}
            errorClass.@process.whitespace = ${' ' + value}
            errorClass.@if.hasValicationErrors = ${elementHasValicationErrors}
        }
        id = ${element.uniqueIdentifier}
    }
}

prototype(Wwwision.Form.FusionPreset:FormElementValidationErrors) < prototype(Neos.Fusion:Tag) {
    tagName = 'span'
    attributes {
        class = 'help-inline'
    }
    content = Neos.Fusion:Collection {
        collection = ${elementValicationErrors}
        itemName = 'error'
        itemRenderer =  ${error}
    }
    @if.hasValidationErrors = ${elementHasValicationErrors}
}

prototype(Wwwision.Form.FusionPreset:FormElementDescription) < prototype(Neos.Fusion:Tag) {
    tagName = 'span'
    attributes {
        class = 'help-block'
    }
    content = ${element.properties.elementDescription}
    @if.condition = ${element.properties.elementDescription}
}