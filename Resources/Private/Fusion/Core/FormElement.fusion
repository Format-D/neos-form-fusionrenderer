prototype(Neos.Form.FusionRenderer:FormElement) < prototype(Neos.Fusion:Array) {
    @context.elementName = ${'--' + formRuntime.identifier + '[' + element.identifier + ']'}
    @context.elementValue = ${Neos.Form.elementValue(formRuntime, element)}
    @context.elementValidationErrors = ${Neos.Form.validationErrors(formRuntime, element)}
    @context.elementHasValidationErrors = ${Neos.Form.hasValidationErrors(formRuntime, element)}

    label = Neos.Form.FusionRenderer:FormElementLabel
    fieldContainer = Neos.Form.FusionRenderer:FormElementFieldContainer

    @process.renderCallbacks = Neos.Form.FusionRenderer:RenderCallbacks {
        formRuntime = ${formRuntime}
        formElement = ${element}
    }

    @process.wrap = Neos.Fusion:Tag {
        tagName = 'div'
        attributes.class = ${hasValidationErrors ? 'clearfix error' : 'clearfix'}
        content = ${value}
    }

}

prototype(Neos.Form.FusionRenderer:FormElementLabel) < prototype(Neos.Fusion:Tag) {
    tagName = 'label'
    attributes {
        for = ${element.uniqueIdentifier}
    }
    content = Neos.Fusion:Array {
        value = ${Neos.Form.translateAndEscapeProperty(element, 'label')}
        requiredFlag = Neos.Form.FusionRenderer:RequiredFlag
        requiredFlag.@if.isRequired = ${element.required}
    }
}

prototype(Neos.Form.FusionRenderer:RequiredFlag) < prototype(Neos.Fusion:Tag) {
    tagName = 'span'
    attributes.class = 'required'
    content = ${value + '*'}
}

prototype(Neos.Form.FusionRenderer:FormElementFieldContainer) < prototype(Neos.Fusion:Array) {
    field = Neos.Form.FusionRenderer:FormElementField
    validationErrors = Neos.Form.FusionRenderer:FormElementValidationErrors
    elementDescription = Neos.Form.FusionRenderer:FormElementDescription

    @process.wrap = Neos.Fusion:Tag {
        tagName = 'div'
        attributes.class = ${element.properties.containerClassAttribute}
        content = ${value}
    }
}

prototype(Neos.Form.FusionRenderer:FormElementField) < prototype(Neos.Fusion:Tag) {
    attributes {
        placeholder = ${element.properties.placeholder}
        class = Neos.Fusion:Array {
            defaultClass = ${element.properties.elementClassAttribute ? element.properties.elementClassAttribute : null}
            errorClass = ${element.properties.elementErrorClassAttribute ? element.properties.elementErrorClassAttribute : 'error'}
            errorClass.@process.whitespace = ${' ' + value}
            errorClass.@if.hasValidationErrors = ${elementHasValidationErrors}
        }
        id = ${element.uniqueIdentifier}
    }
}

prototype(Neos.Form.FusionRenderer:FormElementValidationErrors) < prototype(Neos.Fusion:Tag) {
    tagName = 'span'
    attributes {
        class = 'help-inline'
    }
    content = Neos.Fusion:Collection {
        collection = ${elementValidationErrors}
        itemName = 'error'
        itemRenderer =  ${error}
    }
    @if.hasValidationErrors = ${elementHasValidationErrors}
}

prototype(Neos.Form.FusionRenderer:FormElementDescription) < prototype(Neos.Fusion:Tag) {
    tagName = 'span'
    attributes {
        class = 'help-block'
    }
    content = ${element.properties.elementDescription}
    @if.condition = ${element.properties.elementDescription}
}