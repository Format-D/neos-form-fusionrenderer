prototype(Neos.Form:Form) < prototype(Neos.Fusion:Tag) {
    tagName = 'form'

    attributes = Neos.Fusion:Attributes {
        method = 'post'
        enctype = 'multipart/form-data'
        id = ${formRuntime.identifier}
        action = ${request.httpRequest.uri + '#' + formRuntime.identifier}
    }

    content = Neos.Fusion:Array {
        formStateHiddenField = Wwwision.Form.FusionPreset:FormStateHiddenField
        pages = Neos.Fusion:Collection {
            collection = ${[formRuntime.currentPage]}
            itemName = 'page'
            itemRenderer = Neos.Fusion:Renderer {
                type = ${page.type}
            }
        }
        navigation = Wwwision.Form.FusionPreset:FormNavigation
    }
    # TODO can this be removed?
    @process.renderCallbacks = Wwwision.Form.FusionPreset:RenderCallbacks {
        formRuntime = ${formRuntime}
    }
}

prototype(Neos.Form:FormEditMode) < prototype(Neos.Form:Form) {
    content.navigation >
    content.pages.collection = ${formRuntime.pages}
}

prototype(Wwwision.Form.FusionPreset:FormStateHiddenField) < prototype(Neos.Fusion:Tag) {
    tagName = 'input'
    attributes {
        type = 'hidden'
        name = ${'--' + formRuntime.identifier + '[__state]'}
        value = ${formRuntime.serializedFormState}
    }
}


prototype(Wwwision.Form.FusionPreset:RenderCallbacks) {
    @class = 'Wwwision\\Form\\FusionPreset\\Fusion\\RenderCallbacksImplementation'
    value = ${value}
}